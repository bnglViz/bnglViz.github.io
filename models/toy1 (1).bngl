# All text following the occurence of '#' character in a line is ignored.
 
# The model consists of a monovalent extracellular ligand, 
# a monovalent cell-surface receptor kinase, and a cytosolic adapter 
# protein. The receptor dimerizes through a receptor-receptor 
# interaction that depends on ligand binding. When two receptors 
# are juxtaposed through dimerization one of the receptor kinases 
# can transphosphorylate the second receptor kinase. 
# Apapter protein A can bind to phosphorylated receptor tyrosine. 


begin parameters
  1 L0   1
  2 R0   1
  3 A0   5
  4 kp1  0.5
  5 km1  0.1
  6 kp2  1.1
  7 km2  0.1
  8 p1  10
  9 d1   5
 10 kpA  1e1
 11 kmA  0.02
end parameters

begin species
  1  L(r)       L0  # Ligand has one site for binding to receptor. 
                    # L0 is initial concentration
  2  R(l,d,Y~U) R0  # Dimer has three sites: l for binding to a ligand, 
                    # d for binding to another receptor, and
                    # Y - tyrosine. Initially Y is unphosphorylated, Y~U.
  3  A(SH2)     A0  # A has a single SH2 domain that binds phosphotyrosine
end species


begin reaction rules

# Ligand binding (L+R)
# Note: specifying r in R here means that the r component must not 
#       be bound.  This prevents dissociation of ligand from R
#       when R is in a dimer.
  1  L(r) + R(l,d) <-> L(r!1).R(l!1,d) kp1, km1

# Aggregation (R-L + R-L)
# Note:  R must be bound to ligand to dimerize.
  2  R(l!+,d) + R(l!+,d) <-> R(l!+,d!2).R(l!+,d!2) kp2, km2

# Transphosphorylation
# Note:  R must be bound to another R to be transphosphorylated.
  3  R(d!+,Y~U) -> R(d!+,Y~P) p1 

# Dephosphorylation
# Note:  R can be in any complex, but tyrosine is not protected by bound A.
  4  R(Y~P) -> R(Y~U) d1

# Adaptor binding phosphotyrosine (reversible). 
# Note: Doesn't depend on whether R is bound to
#       receptor, i.e. binding rate is same whether R is a monomer, is 
#       in association with a ligand, in a dimer, or in a complex.

  5  R(Y~P) + A(SH2) <-> R(Y~P!1).A(SH2!1) kpA, kmA
end reaction rules

begin observables
  Molecules R_dim  R(d!+)      # All receptors in dimer
  Molecules R_phos R(Y~P!?)    # Total of all phosphotyrosines
  Molecules A_R    A(SH2!1).R(Y~P!1) # Total of all A's associated with phosphotyrosines
  Molecules A_tot  A()     # Total of A. Should be a constant during simulation.
  Molecules R_tot  R()     # Total of R. Should be a constant during simulation.
  Molecules L_tot  L()     # Total of L. Should be a constant during simulation.
end observables


generate_network();
writeSBML();
simulate_ode({t_end=>50,n_steps=>20});

# Print concentratons at unevenly spaced times (array-valued parameter)
#simulate_ode({sample_times=>[1,10,100]});
